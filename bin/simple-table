#!/usr/bin/env perl
use warnings;
use strict;
use v5.10.0;

use Text::SimpleTable;
use List::Util qw(max);
use Getopt::Long;

our $opt_tabs = 0;

Getopt::Long::Configure(qw(bundling gnu_compat));
Getopt::Long::GetOptions(
    "tabs" => \$opt_tabs
) or die();

###############################################################################

my $data = [];

while (<>) {
    s{\R\z}{};
    if ($opt_tabs) {
        push(@$data, [split("\t")]);
    } else {
        push(@$data, [split]);
    }
}

my $maxcols = max map { scalar @$_ } @$data;

my @column_lengths = ();
for (my $i = 0; $i < $maxcols; ++$i) {
    my $column_length = max map { defined $_->[$i] ? length $_->[$i] : 0 } @$data;
    push(@column_lengths, $column_length);
}

my $t = Text::SimpleTable->new(@column_lengths);
foreach my $row (@$data) {
    $t->row(@$row);
}
print $t->draw();
